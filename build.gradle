import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java'
    id "com.vanniktech.maven.publish" version "0.23.1"
}

group 'cn.guangchen233'
version '2.0.0-SNAPSHOT'

repositories {
    maven {
        url = 'https://repo.huaweicloud.com/repository/maven/'
    }

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        url = 'https://oss.sonatype.org/content/repositories/central'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
}

// Config

javadoc {
    options.encoding "UTF-8"
}

// Tasks

tasks.register("PackSourcesJar", Jar) {
    from(sourceSets.main.allJava)
    archiveClassifier.set("sources")
}

tasks.register("PackJavadocJar", Jar) {
    from(javadoc)
    archiveClassifier.set("javadoc")
}

tasks.register("CleanJavadocDir", Delete) {
    delete("docs/")
}

tasks.register("GenJavadocDir", Copy) {
    dependsOn(tasks.named("CleanJavadocDir"))

    from(javadoc)
    into("docs/")
}

// Tasks dependencies
tasks.named("build") {
    dependsOn(tasks.named("GenJavadocDir"))
}

tasks.named("clean", Delete) {
    dependsOn(tasks.named("CleanJavadocDir"))
}

tasks.named("jar", Jar) {
    dependsOn([
            tasks.named("PackJavadocJar"),
            tasks.named("PackSourcesJar")
    ])
}

// Publish

afterEvaluate {
    mavenPublishing {
        coordinates(
                "io.github.guangchen2333",
                "$rootProject.name",
                "$version"
        )

        // https://s01.oss.sonatype.org
        publishToMavenCentral(SonatypeHost.S01)

        signAllPublications()

        pom {
            name = "Crystal Kill Listener"
            description = "Add an event on Spigot API to listen to players kill a player with EnderCrystal"
            url = "https://github.com/GuangChen2333/CrystalKillListener"
            inceptionYear = "2022"

            licenses {
                license {
                    name = "MIT License"
                    url = 'https://opensource.org/licenses/MIT'
                    distribution = "https://raw.githubusercontent.com/GuangChen2333/CrystalKillListener/dev/LICENSE"
                }
            }

            developers {
                developer {
                    id = "GuangChen2333"
                    name = "Guang_Chen_"
                    email = "guangchenworks@outlook.com"
                    url = "https://github.com/GuangChen2333/"
                }
            }

            scm {
                url = 'https://github.com/GuangChen2333/CrystalKillListener'
                connection = 'scm:git:git://github.com/GuangChen2333/CrystalKillListener.git'
                developerConnection = 'scm:git:ssh://git@github.com:GuangChen2333/CrystalKillListener.git'
            }
        }
    }
}