plugins {
    id 'java'
    id 'maven-publish'
}

group 'cn.guangchen233'
version '1.3.1-SNAPSHOT'

repositories {
    maven {
        url = 'https://repo.huaweicloud.com/repository/maven/'
    }

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        url = 'https://oss.sonatype.org/content/repositories/central'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
}

javadoc {
    options.encoding "UTF-8"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set("sources")
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
}

task cleanJavadocDir(type: Delete) {
    delete "docs/"
}

task javadocDir(type: Copy) {
    dependsOn cleanJavadocDir
    from javadoc
    into('docs/')
}

jar.dependsOn([
        sourcesJar,
        javadocJar,
        javadocDir
])

clean.dependsOn(
        cleanJavadocDir
)

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.guangchen2333'
            artifactId = "$rootProject.name"
            version = "$version"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "Crystal Kill Listener"
                description = "Add an event on Spigot API to listen to players kill a player with EnderCrystal"
                url = "https://github.com/GuangChen2333/CrystalKillListener"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        name = 'GuangChen2333'
                        email = 'guangchen2333@outlook.com'
                    }
                }
                scm {
                    url = 'https://github.com/GuangChen2333/CrystalKillListener'
                    connection = 'scm:git:git://github.com/GuangChen2333/CrystalKillListener.git'
                    developerConnection = 'scm:git:ssh://git@github.com:GuangChen2333/CrystalKillListener.git'
                }
            }
        }
    }

    repositories {
        maven {
            if (project.version.endsWith('-SNAPSHOT')) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }

            if (file("gradle.properties").exists()) {
                credentials {
                    username = "$sonatypeUsername"
                    password = "$sonatypePassword"
                }
            } else {
                println("[Info] the password file does not exist")
            }

        }
    }
}